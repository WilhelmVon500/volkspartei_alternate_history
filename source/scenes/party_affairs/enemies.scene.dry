title: Choosing Our Enemies
new-page: true
view-if: year <= 1930 and enemies_timer <= 0
on-arrival: month_actions += 1; enemies_timer = 18
max-visits: 3
is-card: true
card-image: img/poster_1.png
tags: party_affairs

= Choosing our enemies

In our media and campaign materials, who should we consider as our main opponents?

[? if enemies != 0 : Previously, we had considered the [+ enemies +] as our prior enemies. Is a change of direction necessary? ?]

- @anti_dem: The anti-democratic parties of the far right and left - the KPD, NSDAP, and DNVP.
- @nsdap: The NSDAP alone.
- @kpd: The KPD.
- @socialist: The socialist parties - the SPD and KPD.
- @easy_discard

@anti_dem
on-arrival: enemies = "anti-democratic parties"; liberal_strength += 5; liberal_dissent -= 5; nationalist_strength -= 3; nationalist_dissent += 5; workers_kpd -= 2; new_middle_nsdap -= 2; rural_dnvp -= 2; DNVP_relation -= 7; NSDAP_relation -= 7
choose-if: liberal_strength > nationalist_strength + industrialist_strength - 10

Pushing against both the far-right and far-left appeases our liberal wing, though the industrialists and nationalists would prefer to solely go against the Marxist parties. The Weimar Parties also appear to soften their opposition against our more nationalist tendencies, if only slightly.

@nsdap
on-arrival: enemies = "NSDAP"; liberal_strength += 3; liberal_dissent -= 3; nazi_urgency += 1; new_middle_nsdap -= 4;
choose-if: liberal_strength > nationalist_strength

[? if nsdap_votes < 7 : Rallying against a politically moribund party does not rouse our base, and only strengthens the Nazis through the publicity we give them. ?][? if nsdap_votes >= 7 : The Nazisâ€™ rising ascendancy as the party of the right is also eating into our base. We must fight back against the brown tide, even if our parties are both nationalistic by nature. ?]

@kpd
on-arrival: enemies = "KPD"; nationalist_strength += 3; nationalist_dissent -= 3; industrialist_strength += 3; industrialist_dissent -= 3; workers_kpd -= 1; kpd_relation -= 5; DNVP_relation += 3

Demonizing the Communists has emboldened our voter base, though some nationalists and industrialists question our exclusion of the SPD. At the very least, the right is warming up to us.

@socialist
on-arrival: enemies = "socialist parties"; nationalist_strength += 5; nationalist_dissent -= 5; liberal_strength -= 5; liberal_dissent += 5; spd_relation -= 5; kpd_relation -= 5; DNVP_relation += 5; new_middle_spd -= 3; workers_kpd -= 1; coalition_dissent += 1 if in_grand_coalition = 1
choose-if: liberal_strength < nationalist_strength + industrialist_strength - 10

Marxism has no place in Germany, and we will vehemently oppose any of its variants. Our nationalist and industrialist base is fired up, though the Liberals and especially the Stressemanites are growing discontent with our attack on a nominally democratic party.
