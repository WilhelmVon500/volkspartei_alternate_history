title: Political Rally
new-page: true
is-card: true
tags: party_affairs
view-if: rally_timer <= 0
on-arrival: month_actions += 1; rally_timer = 6
card-image: img/protest.jpg

Organizing rallies to rile up their constituents has mainly been a tactic used by the SPD. While our rallies will not have the effectiveness of the socialist parties, simply due to the party’s smaller voter base, we can still galvanize the crowd towards a common message. What will be the primary theme of this rally?

- @attack_republic: Attack the republic’s foundations and gear towards nationalistic rhetoric.
- @need_for_reform: The need for reform, both within the party, and in the nation.
- @democracy: Commitment to the preservation of democracy.
- @local: A mixture of various local and national concerns that resonate most with the audience.
- @easy_discard

@attack_republic
on-arrival: new_middle_dvp += 3*(1-dissent); old_middle_dvp += 2*(1-dissent); nationalist_strength += 5; nationalist_dissent -= 5; liberal_dissent += 5; spd_relation -= 5; ddp_relation -= 5; z_relation -= 3;

[? if black_thursday_seen = 0 or return_to_normalcy = 1: Believers in continued participation within the Republic are disgusted by our shift, and although we have attracted the support of right-wing voters, the nation has not fallen far enough for desperation to truly energize the party in this manner. ?][? if black_thursday_seen = 1 and return_to_normalcy = 0: As the republic continues to fail its people, our message has become wildly popular with the industrialists, and even the DHV has seen an increase of disillusioned white-collar members. ?]

@need_for_reform
on-arrival: new_middle_dvp += 3*(1-dissent); old_middle_dvp += 2*(1-dissent); liberal_strength += 3; labor_strength += 4; liberal_dissent -= 3; labor_dissent -= 3;

The nationalists and industrialists have grown suspicious of our motives, while the democratic parties and especially the SPD has become curious with the possibility of further cooperation. Voters from outside our traditional blocs have even taken an interest with our rally.

@democracy
on-arrival: democratization += 1; pro_republic += 2*(1 - dissent); new_middle_dvp += 3*(1-dissent) if democratization >= 3; old_middle_dvp += 3*(1-dissent) if democratization >= 3; workers_dvp += 2*(1-dissent) if democratization >= 3 and dvp_ideology = "Left"; pro_republic += 4*(1 - dissent) if democratization >= 3 

[? if black_thursday_seen = 0: Maintaining and reinforcing the status quo is popular with most of our party aside from the nationalist-industrialist wing. ?][? if black_thursday_seen = 1 and pro_republic >= 40: Despite the failings of the Republic, we must continue to support the system that we have attached ourselves to. We have restored a portion of faith in the democratic system. ?][? if black_thursday_seen = 1 and pro_republic < 40: With ever-dwindling supporters of the current system, our message has mainly fallen to deaf ears, though a few have changed their minds back to democracy. ?]

@local
on-arrival: old_middle_dvp += 3*(1-dissent); new_middle_dvp += 3*(1-dissent); workers_dvp += 1*(1-dissent) if dvp_ideology = "Left";

Our speakers have gauged the mood and are speaking to the issues that seem to most affect the workers in this moment.
