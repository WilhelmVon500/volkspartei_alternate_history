title: Party Organizations
new-page: true
is-card: true
tags: party_affairs
view-if: party_organizations_timer <= 0
on-arrival: month_actions += 1; party_organizations_timer = 6;
card-image: img/Mann_der_Arbeit.jpg
#subtitle: Outside of electoral politics, the SPD runs a social world onto itself.

= Party Organizations

In addition to being a political party, the SPD is a social world onto itself, with a vast array of associated organizations which serve the social needs of their members from cradle to grave.

- @newspapers: Fund more newspapers and journalists.
- @welfare: Fund more mutual aid and welfare programs.
- @liberal_association: Fund Liberal Association, interparty organization calling for liberal unity.
- @youth: We have the reputation of being a party for old men. Support the youth organizations.
- @nothing: Do not increase funding for any program.
- @easy_discard

@newspapers
choose-if: resources > 0
unavailable-subtitle: Not enough resources.
subtitle: -1 resources
on-arrival: resources -= 1; media_timer = 0;
go-to: media

# TODO: differing outcomes based on whether you've seen media, and what kind of media you've supported.


@welfare
choose-if: resources > 0
unavailable-subtitle: Not enough resources.
subtitle: -1 resources
on-arrival: resources -= 1; workers_aid += 1; unemployed_dvp += 3*(1 - dissent);

We have increased funding to Workers' Welfare. [? if black_thursday_seen > 0 : In an age of economic crisis, this will be helpful for improving our standing among the unemployed. ?]

@liberal_association
choose-if: resources > 0
view-if: not lvp_formed
unavailable-subtitle: Not enough resources.
subtitle: -1 resources
on-arrival: resources -= 1; liberal_association += 1; nationalist_dissent += 7; liberal_dissent -= 7; liberal_strength += 7; ddp_relation += 3

We have supported the Liberal Association, strengthening the movement for a united liberal party.

@youth
choose-if: resources > 0
unavailable-subtitle: Not enough resources.
subtitle: -1 resources
on-arrival: resources -= 1; young_liberals += 1; liberal_strength += 5; neorevisionist_strength += 5;

Our new youth members flock to the left or neorevisionist wings of the party, and also bring new radicalism to the Reichsbanner.

@nothing

We do not fund any party organizations.

- @return: Continue...
