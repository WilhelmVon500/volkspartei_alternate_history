title: Media
new-page: true
is-card: true
tags: party_affairs
view-if: media_timer <= 0
on-arrival: month_actions += 1; media_timer += 6;
card-image: img/poster_4.jpg

= Media

The [? if not lvp_formed: <span style="color: #C0A054;">**[+ dvp_name +]**</span> ?][? if lvp_formed: <span style="color: #FFA500;">**[+ dvp_name +]**</span> ?] has the fewest supportive newspapers of any political party â€” this has been a significant weakness for us. What actions can we take to remedy this situation?

- @campaign
- @write_editorials
- @newspaper
- @newspaper_2
- @newspaper_3
- @return: We should not do anything with media at the moment.
- @easy_discard
- @cancel_advisor_action

@campaign
title: Campaign for our party.
subtitle: We will coordinate our campaign with supportive newspapers.
on-arrival: campaign_media += 1; new_middle_dvp += 2*(1 - dissent); old_middle_dvp += 2*(1-dissent);

Our campaign media is mostly reaching the middle class who are already saturated with rightist propaganda, but it must be doing *something*, or at least that is what we hope.

@write_editorials
title: Have leading party figures write editorials in non-partisan newspapers.
on-arrival: liberal_dissent -= 6; labor_dissent -= 6; industrialist_dissent -= 6; nationalist_dissent -= 6; neorevisionist_dissent -= 6

This is not particularly effective in drawing voters, but the ability of the factions to debate over print may reduce internal dissent.

@newspaper
title: We must launch our own newspaper, run by the party.
subtitle: -5 resources
choose-if: resources >= 5
unavailable-subtitle: Not enough resources.
view-if: newspaper = 0
on-arrival: newspaper += 1; resources -= 5; new_middle_dvp += 3*(1 - dissent); old_middle_spd += 3*(1 - dissent); pro_republic += 3*(1-dissent) if democratization > 1

Launching our own newspaper is extremely expensive, but effective in garnering support, especially among the middle class.

@newspaper_2
title: Keep building up our newspaper.
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
view-if: newspaper > 0 and newspaper <= 3
on-arrival: newspaper += 1; resources -= 1; new_middle_dvp += 4*(1 - dissent); old_middle_dvp += 4*(1 - dissent); workers_dvp += 1*(1 - dissent) if dvp_ideology = "Left"; pro_republic += 3*(1-dissent)*(democratization-2) if democratization > 2

The paper continues to draw support for our party, especially among the middle class.

@newspaper_3
title: Keep building up our newspaper.
subtitle: It is now financially self-sufficient, and does not require any more resources.
view-if: newspaper > 3
on-arrival: newspaper += 1; new_middle_dvp += 4*(1 - dissent); old_middle_dvp += 4*(1 - dissent); workers_dvp += 1*(1 - dissent) if dvp_ideology = "Left"; rural_dvp += 2*(1 - dissent); pro_republic += 3*(1-dissent)*(democratization-2) if democratization > 2

The paper continues to draw support for our party, especially among the middle class.
