title: Emergency budget cuts
subtitle: Chancellor [+ chancellor +] proposes significant cuts to social spending.
new-page: true
view-if: (year == 1929 or year == 1930 or year == 1931 or year == 1932) and month >= 12 and chancellor == "BrÃ¼ning" and spd_in_government == 0 and (unemployed > 15 or (year == 1929 and unemployed > 10)) and welfare >= 0 and emergency_cuts_timer = 0
on-arrival: emergency_cuts_seen = 1; emergency_cuts_timer = 12
tags: event
max-visits: 4

= Emergency budget cuts

Due to the crisis of the Great Depression, government budgets have fallen and welfare spending has risen substantially. Chancellor [+ chancellor +] has used Article 48 of the constitution in order to pass an emergency bill cutting welfare spending.

[? if spd_toleration : If SPD rejects this, it would mean the end of the toleration agreement, and [+ chancellor +] would likely dissolve the Reichstag and call new elections. ?]

- @nothing_to_do: There is nothing SPD can do.
- @vote_no
- @vote_yes

@nothing_to_do
view-if: spd_toleration == 0
on-arrival: welfare -= 1; unemployed += 2; unemployed_nsdap += 4; unemployed_kpd += 3; unemployed_spd += 2; pro_republic -= 4; budget += 1; inflation -= 2; economic_growth -= 0.5;

The emergency policy passes under emergency rules, as the Reichstag is currently dissolved. The Nazis use this as a recruiting tool, and so does SPD.


@vote_no
title: Vote against this measure, ending the toleration agreement.
unavailable-subtitle: The SPD will not support this.
view-if: spd_toleration
choose-if: spd_relation < 40
on-arrival: time_to_election = 3; spd_in_government = 0; in_grand_coalition = 0; spd_toleration = 0; pro_republic -= 5
call: set_next_election_time

Social Democrats have broken with the [+ chancellor +] government, and have voted against them in a vote of no confidence. Welfare has been saved, but at what cost?

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].


@vote_yes
title: Vote in favor of this measure to continue the toleration agreement.
unavailable-subtitle: We managed to convince the SPD to vote for this measure!
view-if: spd_toleration
choose-if: spd_relation >= 40
on-arrival: welfare -= 1; unemployed += 2; left_dissent += 10; labor_dissent += 10; workers_spd *= 0.9; unemployed_spd *= 0.9; unemployed_nsdap += 5; unemployed_kpd += 5; workers_nsdap += 5; workers_kpd += 5; workers_spd -= 4 if welfare < 0; unemployed_spd -= 5 if welfare < 0; pro_republic -= 4; kpd_relation -= 8; budget += 1; inflation -= 2; economic_growth -= 0.5;

SPD tries to convince the workers that this policy is necessary in order to maintain a stable parliamentary government, and to deal with the fiscal consequences of the depression. Still, this policy provides excellent recruiting fodder for Nazis and Communists.
