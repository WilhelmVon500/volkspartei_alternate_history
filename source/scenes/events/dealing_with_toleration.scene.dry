title: Dealing with Toleration
new-page: true
view-if: dealing_with_toleration_timer = 0 and spd_toleration
on-arrival: dealing_with_toleration_timer += 6
tags: event

= Dealing with Toleration

<span style="color: #E3000F;">**SPD**</span> is currently acting as external support to the government of Chancellor [+ chancellor +]. Still, they want to somehow use their position.

- @social_spending
- @paramilitary
- @refuse: We refuse their demands.


@social_spending
title: Moderate our austerity politics and increase social spending.
choose-if: budget >= 1
unavailable-subtitle: This can only happen with a budget surplus.
on-arrival: welfare += 1; budget -= 1; workers_spd += 5*(1-dissent); unemployed_spd += 5*(1-dissent); kvp_relation -= 3; dnvp_relation -= 3; spd_relation += 3; progressive_dissent -= 5; democratic_dissent -= 5; industrialist_dissent += 5; nationalist_dissent += 5;

We have increased social spending to appease the <span style="color: #E3000F;">**SPD**</span>.


@paramilitary
view-if: political_decree < 2 and not sa_nationwide_ban
choose-if: spd_prussia and strife >= 3
unavailable-subtitle: [? if not spd_prussia: The <span style="color: #E3000F;">**SPD**</span> doesn't control the Prussian government. ?] [? if strife < 3: Paramilitary violence isn't severe enough to justify an emergency decree. ?]
title: Urge Brüning to grant police stronger actions against political extremists, and to restrict certain rights.
on-arrival: political_decree += 1; prussian_police_loyalty += 0.04; prussian_police_strength += 5; dnvp_relation -= 3 if dnvp_ideology = "Radical"; dnf_relation -= 3 if dnf_formed; nsdap_relation -= 6; rfb_militancy *= 0.9; rfb_strength *= 0.85; sa_militancy *= 0.96; sa_strength *= 0.96; sh_militancy *= 0.98; sh_strength *= 0.98; workers_kpd -= 1; workers_nsdap -= 1; unemployed_kpd -= 3; unemployed_nsdap -= 3; new_middle_nsdap -= 2; hindenburg_angry_bruning += 5

Carl Severing and other Prussian officials have successfully urged Brüning to issue an emergency decree expanding police authority to suppress political extremism. The decree imposes restrictions on political meetings, organizations, newspapers, and posters, all of which now require police approval beforehand.

As a result, several <span style="color: #954B00;">**Nazi**</span> and <span style="color: #8B0000;">**Communist**</span> newspapers have been banned, triggering violent clashes between their supporters and the police. [? if president == "Hindenburg": President Hindenburg has reluctantly signed the decree, but is losing patience with Brüning for yielding to <span style="color: #E3000F;">**socialist**</span> demands. ?]



@refuse
on_arrival: toleration_explode += 1; spd_relation -= 3; workers_dvp -= 2; progressive_dissent += 3;

We will refuse their demands. This may not end well, though...
