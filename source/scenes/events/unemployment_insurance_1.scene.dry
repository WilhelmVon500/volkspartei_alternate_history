title: Unemployment Insurance Crisis
subtitle: The left wing of our coalition objects to proposed finance reforms.
new-page: true
view-if: dvp_in_government = 1 and spd_in_government = 1 and (year = 1930 or year = 1931 or year = 1932) and (month >= 3) and in_grand_coalition = 1 and unemployment_insurance_timer = 0 and (unemployed >= unemployment_insurance_threshold + 4 or (unemployed >= 20 and unemployed >= unemployment_insurance_threshold)) and not constructive_vonc and president == "Hindenburg"
on-arrival: unemployment_insurance_crisis += 1; budget -= 1 if budget >= 1; unemployment_insurance_timer = 10; crisis_urgency += 1; unemployment_insurance_threshold = unemployed
tags: event
max-visits: 2
go-to: what_do if unemployment_insurance_crisis = 2
face-image: img/Kollwitz_Arbeitslosigkeit.jpg

= Unemployment Insurance Crisis

The economic crisis has caused mass layoffs all throughout Germany, affecting the industrial working class the hardest. Our welfare spending is increasing due to the greater need.

In the midst of all this, our party's economic experts in cooperation with those of the <span style="color: #000000;">**Center Party**</span> have devised a plan to cut benefits for the unemployed, lower taxes on the rich, and raise taxes on the working class, as part of a plan to balance the budget and discipline labor. They see these reforms as paramount in order to keep our finances solvent.

The <span style="color: #E3000F;">**SPD**</span>, however, has strenuously objected to the proposed budget modifications, seeing it as an attempt by the bourgeois to dismantle the welfare system they have fought so hard for.

If we attempt to go forward with this plan, they are threatening to blow up the coalition.

They have instead proposed their own measure to maintain budget solvency, which calls for increased taxes on the rich to keep the unemployment insurance program fully funded. Our industrialist backers and middle class base, however, see this as completely unacceptable.

Thankfully, President Hindenburg is on our side, and will likely support any attempts at passing these reforms by emergency decree if the government were to collapse.

[? if unemployment_insurance_crisis = 2 : This is the second time our finances have been imperiled thusly. Obviously, the previous reforms have not sufficiently disciplined labor to allow the economy to recover. ?]

- @tax_poor: Why can't we simply tax the workers?

@tax_poor
max-visits: 1
go-to: what_do

The <span style="color: #E3000F;">**SPD**</span>, as the representatives of the working class, would not have that.

@what_do

But the crux of the matter is, what can we do?

- @destroy: Fine. We will end this coalition.
- @accept: Unfortunately, we must accept the Right's demands for the sake of stability.
- @compromise: Surely there is some compromise to be had?

@destroy
call: set_next_election_time
on-arrival: time_to_election = 3; spd_in_government = 0; in_grand_coalition = 0; spd_toleration = 0; spd_relation -= 10; labor_dissent += 5; progressive_dissent += 7; industrialist_strength += 7; chancellor = "Br端ning"; chancellor_party = "Z "; welfare -= 2; budget += 1; inflation -= 2; foreign_minister_party = "DVP"; interior_minister_party = "Z"; justice_minister_party = "I"; labor_minister_party = "Z"; reichswehr_minister_party = "I"; economic_minister_party = ddp_name; finance_minister_party = "DVP"; agriculture_minister_party = "Z"; spd_no_confidence = 1; pro_republic -= 4; old_middle_spd -= 3; new_middle_spd -= 2; rural_spd -= 2; catholics_spd -= 2; economic_growth -= 0.6

Fine. If the socialists wish to blow up the government, let them do so. Hindenburg is on our side, and so we do not need them.

[? if chancellor_party = "SPD" : Heinrich Br端ning of the <span style="color: #000000;">**Center Party**</span> has been appointed Chancellor. ?] Br端ning now leads an all-bourgeois cabinet. Using the emergency powers of Article 48, Br端ning and President Hindenburg have passed the welfare cuts over the objections of the Reichstag.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@accept
unavailable-subtitle: The industrialist wing of our party would never allow this.
choose-if: False
on-arrival: new_middle_dvp -= 0.15*new_middle_dvp; old_middle_dvp -= 0.25*old_middle_dvp; workers_spd -= 0.1*workers_spd; unemployed_spd -= 0.25*unemployed_spd; workers_kpd += 7; workers_nsdap += 7; unemployed_kpd += 5; unemployed_nsdap += 5; industrialist_dissent += 15; nationalist_dissent += 15; labor_strength += 10; democratic_strength += 7; progressive_strength += 7; welfare -= 2; budget += 1; unemployed += 3; month += 1; pro_republic -= 4; dnvp_relation -= 8; inflation -= 2; economic_growth -= 0.6

Unfortunately, we must accept this ultimatum if we are to maintain this coalition. The Nazis and Communists are waiting in the wings for us to fail, ready to pounce upon the instability that would result from a breakup of the governing coalition.

The industrialists and small business owners are furious at our betrayal, as are a good portion of our membership throughout the factions. Our loss is the DNVP's gain, and even the NSDAP, who heavy industry see as the only people willing to stand up to the socialists.


@compromise
unavailable-subtitle: [? if spd_relation < 35 : We do not have good enough relations with the <span style="color: #E3000F;">**SPD**</span>. ?] [? if dvp_ideology != "Left" : The party is too right-wing to consider compromises with the socialists. ?]
choose-if: spd_relation >= 35 and dvp_ideology = "Left"

Thanks to our good relations, the <span style="color: #E3000F;">**SPD**</span> has offered a compromise that raises some taxes on the wealthy, along with lesser welfare cuts. Our members have mixed opinions on accepting this compromise. The Liberals support maintaining our government to keep the fascists and communists out, while Labor of course supports any measures to support the workers in an otherwise bourgeois party, but the Industrialists who provide the bulk of the funding our party needs to function are strongly opposed.

- @accept_compromise: Accept the compromise.
- @destroy: Reject the compromise and let this government fall.

@accept_compromise
on-arrival: new_middle_dvp -= 0.1*new_middle_dvp; old_middle_dvp -= 0.15*new_middle_dvp; workers_spd -= 2; unemployed_spd -= 2; workers_kpd += 3; unemployed_kpd += 2; unemployed_nsdap += 2; industrialist_dissent += 10; nationalist_dissent += 10; labor_strength += 7; liberal_strength += 5; democratic_strength += 5; progressive_strength += 5; welfare -= 1; budget += 1; month += 1; dnvp_relation -= 4; inflation -= 1; economic_growth -= 0.3

We have accepted the compromise offered by the socialists. Many our industrialist backers and middle class supporters see this as a betrayal, although we present it as the successful aversion of the collapse of our government.
