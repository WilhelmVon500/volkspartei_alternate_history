title: Presidential Election
subtitle: Hindenburg runs for re-election...
view-if: year = 1932 and month >= 3 and presidential_election_1932_seen = 0
frequency: 1000
max-visits: 1
new-page: true
on-arrival: presidential_election_1932_seen = 1; next_presidential_election_year = year + 7; next_presidential_election_month = month;
tags: event
go-to: election_algorithm
set-jump: pres_election
face-image: img/hindenburg_election.jpg

@pres_election

The presidential election's first round is set to take place this month. If no candidate secures a majority, a second round will be held, with the candidate receiving the most votes winning the presidency.

- @final_election: The first round results...

@final_election
on-arrival: election_round = 0
go-to: election_algorithm
set-jump: post_election



@post_election
on-arrival: {!
Q.hindenburg_votes = 0;
Q.braun_votes = 0;
Q.harzburg_votes = 0;
Q.hitler_votes = 0;
Q.thalmann_votes = 0;

// initial phase:
Q.hitler_votes += Q.nsdap_normalized;
Q.thalmann_votes += Q.kpd_normalized;

if (Q.dnvp_candidate == "Duesterberg") {
    Q.harzburg_votes += Q.dnvp_normalized;
}


if (Q.spd_candidate == "Hindenburg") {
    Q.hindenburg_votes += Q.spd_normalized;
}
if (Q.ddp_candidate == "Hindenburg" && Q.ddp_exist) {
    Q.hindenburg_votes += Q.ddp_normalized;
}
if (Q.dbp_candidate == "Hindenburg" && Q.dbp_exist) {
    Q.hindenburg_votes += Q.dbp_normalized;
}
if (Q.z_candidate == "Hindenburg") {
    Q.hindenburg_votes += Q.z_normalized;
}
if (Q.dvp_candidate == "Hindenburg") {
    Q.hindenburg_votes += Q.dvp_normalized;
}
if (Q.csvd_candidate == "Hindenburg" && Q.csvd_exist) {
    Q.hindenburg_votes += Q.csvd_normalized;
}
if (Q.wp_candidate == "Hindenburg" && Q.wp_exist) {
    Q.hindenburg_votes += Q.wp_normalized;
}
if (Q.cnblp_candidate == "Hindenburg" && Q.cnblp_exist) {
    Q.hindenburg_votes += Q.cnblp_normalized;
}
if (Q.kvp_candidate == "Hindenburg" && Q.kvp_formed) {
    Q.hindenburg_votes += Q.kvp_normalized;
}
if (Q.dnvp_candidate == "Hindenburg") {
    Q.hindenburg_votes += Q.dnvp_normalized;
}
if (Q.other_candidate == "Hindenburg") {
    Q.hindenburg_votes += Q.other_normalized;
}


if (Q.braun_campaign) {
    Q.braun_votes += Q.spd_normalized;
    if (Q.z_candidate == "Braun") {
        Q.braun_votes += Q.z_normalized;
    }
    if (Q.ddp_candidate == "Braun" && Q.ddp_exist) {
        Q.braun_votes += Q.ddp_normalized;
    }
    if (Q.dbp_candidate == "Braun" && Q.dbp_exist) {
        Q.braun_votes += Q.dbp_normalized;
    }
    if (Q.dvp_candidate == "Braun") {
        Q.braun_votes += Q.dvp_normalized;
    }
}

// if (Q.hindenburg_to_braun_bonus) {
//     Q.hindenburg_votes -= Q.hindenburg_to_braun_bonus;
//     Q.braun_votes += Q.hindenburg_to_braun_bonus;
// }
// if (Q.thalmann_to_braun_bonus) {
//     Q.thalmann_votes -= Q.thalmann_to_braun_bonus;
//     Q.braun_votes += Q.thalmann_to_braun_bonus;
// }

// check for majority
Q.hindenburg_votes = Math.round(Q.hindenburg_votes*1000)/10;
Q.braun_votes = Math.round(Q.braun_votes*1000)/10;
Q.harzburg_votes = Math.round(Q.harzburg_votes*1000)/10;
Q.hitler_votes = Math.round(Q.hitler_votes*1000)/10;
Q.thalmann_votes = Math.round(Q.thalmann_votes*1000)/10;

Q.hindenburg_majority = (Q.hindenburg_votes >= 50);
Q.braun_majority = (Q.braun_votes >= 50);
Q.thalmann_majority = (Q.thalmann_votes >= 50);
Q.harzburg_majority = (Q.harzburg_votes >= 50);
Q.hitler_majority = (Q.hitler_votes >= 50);
Q.any_majority = Q.hindenburg_majority | Q.braun_majority | Q.harzburg_majority | Q.thalmann_majority | Q.hitler_majority;

// check for plurality
Q.hindenburg_plurality = (Q.hindenburg_votes >= Q.hitler_votes && Q.hindenburg_votes >= Q.braun_votes && Q.hindenburg_votes >= Q.thalmann_votes && Q.hindenburg_votes >= Q.harzburg_votes);
Q.braun_plurality = (Q.braun_votes >= Q.hindenburg_votes && Q.braun_votes >= Q.hitler_votes && Q.braun_votes >= Q.thalmann_votes && Q.braun_votes >= Q.harzburg_votes);
Q.harzburg_plurality = (Q.harzburg_votes >= Q.hindenburg_votes && Q.harzburg_votes >= Q.braun_votes && Q.harzburg_votes >= Q.hitler_votes && Q.harzburg_votes >= Q.thalmann_votes);
Q.thalmann_plurality = (Q.thalmann_votes >= Q.hindenburg_votes && Q.thalmann_votes >= Q.braun_votes && Q.thalmann_votes >= Q.hitler_votes && Q.thalmann_votes >= Q.harzburg_votes);
Q.hitler_plurality = (Q.hitler_votes >= Q.hindenburg_votes && Q.hitler_votes >= Q.braun_votes && Q.hitler_votes >= Q.thalmann_votes && Q.hitler_votes >= Q.harzburg_votes);
// If no candidate gets above 50%, then there's a second round. In the second round, a plurality wins. 
Q.election_round += 1;

// making the vote numbers nicer for the display
Q.hindenburg_votes_disp = Q.hindenburg_votes.toFixed(1);
Q.braun_votes_disp = Q.braun_votes.toFixed(1);
Q.harzburg_votes_disp = Q.harzburg_votes.toFixed(1);
Q.hitler_votes_disp = Q.hitler_votes.toFixed(1);
Q.thalmann_votes_disp = Q.thalmann_votes.toFixed(1);
!}
go-to: hindenburg_wins if hindenburg_majority == 1; thalmann_wins if thalmann_majority == 1; hitler_wins if hitler_majority == 1; braun_wins if braun_majority == 1; no_majority_round1 if any_majority == 0 and election_round == 1

Results:

Hindenburg - [+ hindenburg_votes_disp +]%

[+ nsdap_candidate +] - [+ hitler_votes_disp +]%

[? if dnvp_candidate = "Duesterberg": Duesterberg - [+ harzburg_votes_disp +]% ?]

Thälmann - [+ thalmann_votes_disp +]%

[? if braun_campaign: Braun - [+ braun_votes_disp +]% ?]

@no_majority_round1
on-arrival: braun_r1 = 1 if braun_campaign; duesterberg_r1 = 1 if dnvp_candidate = "Duesterberg"; dnvp_candidate = "Hindenburg" if dnvp_candidate = "Duesterberg"; nsdap_candidate = "Hindenburg" if (braun_plurality or thalmann_plurality); braun_campaign = 0 if (hitler_plurality or harzburg_plurality); spd_candidate = "Hindenburg" if (braun_campaign = 0 and spd_candidate = "Braun"); ddp_candidate = "Hindenburg" if (braun_campaign = 0 and ddp_candidate = "Braun" and ddp_exist); dbp_candidate = "Hindenburg" if (braun_campaign = 0 and dbp_candidate = "Braun" and dbp_exist); z_candidate = "Hindenburg" if (braun_campaign = 0 and z_candidate = "Braun");
go-to: round2_menu

There is no majority in this round. In the second round, any candidate who wins a plurality will win overall. What should we do?

[? if duesterberg_r1: Following last month’s events, Duesterberg has withdrawn from the race. ?]

[? if braun_plurality or thalmann_plurality : In order to consolidate the vote for the Right, and to prevent a socialist from becoming president, [+ nsdap_candidate +] is no longer running in the second round, and has switched his endorsement to Hindenburg. ?]

[? if (hitler_plurality or harzburg_plurality) and braun_r1: In order to consolidate the vote for the Republicans, and to prevent a rightist from becoming president, Braun is no longer running in the second round, and has switched his endorsement to Hindenburg. ?]


@round2_menu

- @round2_convince_z_braun: Convince the Center to switch their support to us.
- @round2_convince_z_hindenburg: Convince the Center to switch their support to us.
- @round2_convince_ddp_braun: Convince the [+ ddp_name +] to switch their support to us.
- @round2_convince_ddp_hindenburg: Convince the [+ ddp_name +] to switch their support to us.
- @round2_convince_dbp_braun: Convince the DBP to switch their support to us.
- @round2_convince_dbp_hindenburg: Convince the DBP to switch their support to us.
- @round2_convince_spd_hindenburg: Convince the SPD to switch their support to us.
- @round2_hindenburg: Switch our support to Hindenburg.
- @round2_braun: Switch our support to Braun.
- @round2_vote: Let the second round voting begin.




@round2_convince_z_braun
view-if: z_candidate != dvp_candidate and dvp_candidate = "Braun"
go-to: r2_z_failed_braun if z_relation < 45; r2_z_success_braun if z_relation >= 45
max-visits: 1

@r2_z_success_braun
go-to: round2_menu
on-arrival: nsdap_candidate = "Hindenburg" if braun_votes + z_votes > hitler_votes; z_candidate = "Braun"; braun_votes += z_votes

[? if nsdap_candidate = "Hindenburg" : The Center's opinion is affected by the Nazis' support of Hindenburg, and is willing to turn away from him. ?] The Center are convinced! They will be part of the Braun coalition.

[? if not nsdap_candidate = "Hindenburg" and braun_votes + z_votes > hitler_votes: With the Center Party supporting Braun, [+ nsdap_candidate +] has dropped out of the race, and the Nazis have endorsed Hindenburg to prevent a socialist from becoming president. ?]

@r2_z_failed_braun
go-to: round2_menu

Unfortunately, we fail to turn them away from Hindenburg.


@round2_convince_z_hindenburg
view-if: z_candidate != dvp_candidate and dvp_candidate = "Hindenburg"
go-to: r2_z_failed_hindenburg if z_relation < 45; r2_z_success_hindenburg if z_relation >= 45
max-visits: 1

@r2_z_success_hindenburg
go-to: round2_menu
on-arrival: z_candidate = "Hindenburg"

The Center are convinced! They will be part of the Hindenburg coalition.

@r2_z_failed_hindenburg
go-to: round2_menu

Unfortunately, we fail to turn them away from Braun.



@round2_convince_ddp_braun
view-if: ddp_candidate != dvp_candidate and dvp_candidate = "Braun" and ddp_exist
go-to: r2_ddp_failed_braun if ddp_relation < 35; r2_ddp_success_braun if ddp_relation >= 35
max-visits: 1

@r2_ddp_success_braun
go-to: round2_menu
on-arrival: nsdap_candidate = "Hindenburg" if braun_votes + ddp_votes > hitler_votes; ddp_candidate = "Braun"; braun_votes += ddp_votes

The [+ ddp_name +] are convinced! They will be part of the Hindenburg coalition.

[? if not nsdap_candidate = "Hindenburg" and braun_votes + ddp_votes > hitler_votes: With the [+ ddp_name +] supporting Braun, [+ nsdap_candidate +] has dropped out of the race, and the Nazis have endorsed Hindenburg to prevent a socialist from becoming president. ?]


@r2_ddp_failed_braun
go-to: round2_menu

Unfortunately, we fail to turn them away from Hindenburg.


@round2_convince_ddp_hindenburg
view-if: ddp_candidate != dvp_candidate and dvp_candidate = "Hindenburg" and ddp_exist
go-to: r2_ddp_failed_hindenburg if ddp_relation < 45; r2_ddp_success_hindenburg if ddp_relation >= 45
max-visits: 1

@r2_ddp_success_hindenburg
go-to: round2_menu
on-arrival: ddp_candidate = "Hindenburg"

The [+ ddp_name +] are convinced! They will be part of the Hindenburg coalition.

@r2_ddp_failed_hindenburg
go-to: round2_menu

Unfortunately, we fail to turn them away from Braun.



@round2_convince_dbp_braun
view-if: dbp_candidate != dvp_candidate and dvp_candidate = "Braun" and dbp_exist
go-to: r2_dbp_failed_braun if dbp_relation < 35; r2_dbp_success_braun if dbp_relation >= 35
max-visits: 1

@r2_dbp_success_braun
go-to: round2_menu
on-arrival: nsdap_candidate = "Hindenburg" if braun_votes + dbp_votes > hitler_votes; dbp_candidate = "Braun"; braun_votes += dbp_votes

The DBP are convinced! They will be part of the Hindenburg coalition.

[? if not nsdap_candidate = "Hindenburg" and braun_votes + dbp_votes > hitler_votes: With the DBP supporting Braun, [+ nsdap_candidate +] has dropped out of the race, and the Nazis have endorsed Hindenburg to prevent a socialist from becoming president. ?]


@r2_dbp_failed_braun
go-to: round2_menu

Unfortunately, we fail to turn them away from Hindenburg.


@round2_convince_dbp_hindenburg
view-if: dbp_candidate != dvp_candidate and dvp_candidate = "Hindenburg" and dbp_exist
go-to: r2_dbp_failed_hindenburg if dbp_relation < 45; r2_dbp_success_hindenburg if dbp_relation >= 45
max-visits: 1

@r2_dbp_success_hindenburg
go-to: round2_menu
on-arrival: dbp_candidate = "Hindenburg"

The DBP are convinced! They will be part of the Hindenburg coalition.

@r2_dbp_failed_hindenburg
go-to: round2_menu

Unfortunately, we fail to turn them away from Braun.


@round2_convince_spd_hindenburg
view-if: spd_candidate != dvp_candidate and dvp_candidate = "Hindenburg"
go-to: r2_spd_failed_hindenburg if spd_relation < 45; r2_spd_success_hindenburg if spd_relation >= 45
max-visits: 1

@r2_spd_success_hindenburg
go-to: round2_menu
on-arrival: spd_candidate = "Hindenburg"; braun_campaign = 0; z_candidate = "Hindenburg" if z_candidate = "Braun"; dbp_candidate = "Hindenburg" if dbp_candidate = "Braun"; ddp_candidate = "Hindenburg" if ddp_candidate = "Braun"; braun_votes = 0

The SPD are convinced! They will be part of the Hindenburg coalition.

@r2_spd_failed_hindenburg
go-to: round2_menu

Unfortunately, we fail to turn them away from Braun.





@round2_hindenburg
view-if: dvp_candidate != "Hindenburg"
on-arrival: dvp_candidate = "Hindenburg"

We have switched our support to Hindenburg.

@round2_braun
view-if: dvp_candidate != "Braun" and braun_campaign
on-arrival: dvp_candidate = "Braun"

We have switched our support to Braun.

- @round2_vote: Second round voting begins next month.

@round2_vote

Hopefully Germany makes the right choice.


@hindenburg_wins
on-arrival: president = "Hindenburg"

As expected, the incumbent Hindenburg wins the presidency.

@thalmann_wins
on-arrival: president = "Thälmann";

Ernst Thälmann of the KPD has won the presidency.

- @civil_war: This is civil war.

@hitler_wins
on-arrival: president = nsdap_candidate; chancellor = "Hitler"; chancellor_party = "NSDAP"

[? if nsdap_candidate == "Hitler" : Adolf Hitler of the NSDAP has won the presidency. He wastes no time in consolidating power. ?]
[? if nsdap_candidate != "Hitler" : The NSDAP candidate [+ president +] has won. The first action President [+ president +] takes is inviting Hitler back into Germany, and appointing him chancellor. The Nazis waste no time in consolidating power. ?]

- @hitler_takes_power: What do we do now?


@braun_wins
on-arrival: president = "Braun"; workers_spd += 5; old_middle_spd += 3; new_middle_spd += 3; rural_spd += 3; unemployed_spd += 3; pro_republic += 3

Otto Braun of the SPD has won the presidency! This is a moment of celebration for their party, and his victory is able to bolster their support.
