title: Economic Policy
new-page: true
is-card: true
tags: govt_affairs, cabinet
on-arrival: economic_policy_timer += 12; month_actions += 1
view-if: dvp_in_government = 1 and (economic_minister_party == dvp_name or finance_minister_party == dvp_name) and economic_policy_timer <= 0 and economic_plan > 0
card-image: img/portraits/SchmidtRobert.jpg
#subtitle: Policies to address the economic crisis.

= Economic Policy

We have already formulated an economic plan. Now we have to implement it.

- @schacht_plan
- @lautenbach_plan
- @root: Do not implement the economic plan.
- @easy_discard
- @cancel_advisor_action



#######################
# Schacht economic plan

@schacht_plan
title: Implement the Schacht plan.
subtitle: -2 budget. This violates Treaty of Versailles
unavailable-subtitle: -2 budget. [? if budget < 2 : We do not have the necessary budget. ?] [? if unemployed < 8 : Unemployment is low enough that job creation is not necessary. ?]
view-if: schacht_plan_adopted == 1
choose-if: budget >= 2 and unemployed >= 8
on-arrival: budget -= 2; works_program += 1; unemployed -= 4 if unemployed >= 8; inflation += 1.5; workers_nsdap -= 2*(1-dissent); unemployed_nsdap -= 3*(1-dissent); old_middle_nsdap -= 2*(1-dissent); workers_dvp += 2*(1-dissent); unemployed_dvp += 1*(1-dissent); new_middle_dvp += 2*(1-dissent); old_middle_dvp += 2*(1-dissent); rural_dvp += 1*(1-dissent); schacht_plan_progress += 1; inflation += 1 if unemployed < 10; economic_growth += 2; pro_republic += 3*(1-dissent); ddp_relation += 3; spd_relation -= 1; west_relation -= 1; east_relation -= 1;
achievement: wirtschaftspolitik

[? if schacht_plan_progress == 1 : We have started to implement the job-creation plan! Such aggressive economic expansion violates Treaty of Versailles, angering the west and the east. ?]

[? if schacht_plan_progress > 1 : We have continued to fund the job-creation plan. Such aggressive economic expansion violates Treaty of Versailles, further angering the west and the east. ?]

[? if unemployed < 10 : Unemployment is low enough that the plan does not seem to have the intended effect. ?]

#######################################
# Lautenbach economic plan

@lautenbach_plan
title: Implement the Lautenbach's job-creation plan.
subtitle: -2 budget.
unavailable-subtitle: -2 budget. [? if budget < 2 : We do not have the necessary budget. ?] [? if unemployed < 8 : Unemployment is low enough that job creation is not necessary. ?]
view-if: lautenbach_plan_adopted == 1
choose-if: budget >= 2 and unemployed >= 8
on-arrival: budget -= 2; works_program += 1; unemployed -= 2 if unemployed >= 25; unemployed -= 1; inflation += 1; economic_growth += 1.6; workers_nsdap -= 2*(1-dissent); unemployed_nsdap -= 2*(1-dissent); workers_kpd -= 2*(1-dissent); unemployed_kpd -= 2*(1-dissent); workers_dvp += 2*(1-dissent); unemployed_dvp += 1*(1-dissent); new_middle_dvp += 3*(1-dissent); old_middle_dvp += 2*(1-dissent); rural_dvp += 2*(1-dissent); lautenbach_plan_progress += 1; pro_republic += 3*(1-dissent); z_relation += 3; ddp_relation += 2; spd_relation += 1;
achievement: wirtschaftspolitik

[? if lautenbach_plan_progress == 1 : We have started to implement the job-creation plan! Funds are sent to build public works projects across Germany, reducing unemployment. ?]

[? if lautenbach_plan_progress > 1 : We have continued to fund the job-creation plan, using our funds to build public works throughout Germany. ?]

[? if unemployed >= 10 : The Social Democrats approve of this plan as the "least bad" alternative, seeing it as necessary to save Germany's economy. ?]

[? if unemployed < 10 : Unemployment is low enough that the plan does not seem to have the intended effect. ?]
