title: Otto Hugo
is-pinned-card: true
tags: advisor, nationalist
card-image: img/portraits/HugoOtto.jpg
view-if: hugo_advisor = 1
new-page: true 

= Otto Hugo

Otto Hugo is a prominent member of the right wing in the <span style="color: #C0A054;">**[+ dvp_name +]**</span>, known for his nationalist views and cooperative stance towards far-right groups. [? if dvp_leader = "Hugo" : He is also the current party leader. ?]

- @building_the_right
- @nsdap_cooperation
- @against_toleration
- @root: Return to main

@building_the_right
title: Building the Right
subtitle: Push the <span style="color: #C0A054;">**[+ dvp_name +]**</span> to the right.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
choose-if: advisor_action_timer = 0
on-arrival: advisor_action_timer = 6; nationalist_strength += 6; nationalist_dissent -= 8; liberal_dissent += 2; dvp_right += 2

We have increased the strength of the nationalist faction with the <span style="color: #C0A054;">**[+ dvp_name +]**</span>.

@nsdap_cooperation
title: Cooperation with the far right
subtitle: Push the party to be more friendly towards the far right parties.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
choose-if: advisor_action_timer = 0
on-arrival: advisor_action_timer = 6; nationalist_strength += 5; dnvp_relation += 5*(1-dissent) if dnvp_leader = "Hugenberg"; nsdap_relation += 3*(1-dissent); liberal_dissent += 5; dvp_right += 1

WIP

@against_toleration
title: Against toleration
view-if: not constructive_vonc
subtitle: Break our current toleration arrangement, and bring down the government via a no-confidence vote.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?] [? if dvp_toleration == 0 : The <span style="color: #C0A054;">**[+ dvp_name +]**</span> is not currently in toleration. ?]
choose-if: advisor_action_timer == 0 and dvp_toleration
on-arrival: advisor_action_timer = 6; next_election_time = time + 3 if next_election_time > time+3; next_election_month = month + 3 if next_election_time >= time + 3; next_election_year = year if next_election_time >= time + 3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; spd_toleration = 0; left_strength += 8; pro_republic -= 4; new_middle_spd -= 3; rural_spd -= 3; old_middle_spd -= 3; catholics_spd -= 3; reformist_dissent += 8

We have grown tired of the burdens of toleration, and have called a vote of no confidence in the current minority government.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].
